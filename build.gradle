apply plugin: 'kotlin'
apply plugin: 'java-library'
apply plugin: 'application'
apply plugin: 'org.jlleitschuh.gradle.ktlint'

buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        gradlePluginPortal()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:10.2.0"
        // This follows advice from https://blog.gradle.org/log4j-vulnerability
        constraints {
            classpath("org.apache.logging.log4j:log4j-core") {
                version {
                    strictly("[2.17, 3[")
                    prefer("2.17.0")
                }
                because("CVE-2021-44228, CVE-2021-45046, CVE-2021-45105: Log4j vulnerable to remote code execution and other critical security vulnerabilities")
            }
        }

    }
}

version = "1.10.0.15-SNAPSHOT"
mainClassName = 'gdx.liftoff.MainKt'
sourceCompatibility = JavaVersion.VERSION_1_8

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
    from { configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    exclude('META-INF/INDEX.LIST', 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA')
    dependencies {
        exclude('META-INF/INDEX.LIST', 'META-INF/maven/**')
    }
    doLast {
        file(archiveFile).setExecutable(true, false)
    }
}

repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://s01.oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://s01.oss.sonatype.org" }
    gradlePluginPortal()
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    implementation "org.apache.commons:commons-exec:$commonsExecVersion"
    implementation "com.badlogicgames.gdx:gdx:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    implementation "com.crashinvaders.lml:gdx-kiwi:$lmlVersion"
    implementation "com.crashinvaders.lml:gdx-lml:$lmlVersion"
    implementation "com.crashinvaders.lml:gdx-lml-vis:$lmlVersion"
    implementation "com.crashinvaders.lml:gdx-autumn:$lmlVersion"
    implementation "com.crashinvaders.lml:gdx-autumn-fcs:$lmlVersion"
    implementation "com.crashinvaders.lml:gdx-autumn-mvc:$lmlVersion"
    implementation "com.kotcrab.vis:vis-ui:$visUiVersion"

    implementation "org.lwjgl:lwjgl-nfd:$lwjgl3Version"
    implementation "org.lwjgl:lwjgl-nfd:$lwjgl3Version:natives-windows"
    implementation "org.lwjgl:lwjgl-nfd:$lwjgl3Version:natives-windows-x86"
    implementation "org.lwjgl:lwjgl-nfd:$lwjgl3Version:natives-linux"
    implementation "org.lwjgl:lwjgl-nfd:$lwjgl3Version:natives-macos"
    implementation "org.lwjgl:lwjgl-nfd:$lwjgl3Version:natives-macos-arm64"
    implementation "org.lwjgl:lwjgl-nfd:$lwjgl3Version:natives-windows"
    implementation "org.lwjgl:lwjgl-nfd:$lwjgl3Version:natives-windows-x86"

    implementation "io.github.lukehutch:fast-classpath-scanner:2.21"
    implementation "khttp:khttp:1.0.0"
    implementation "com.github.devcsrj:mvnrepository-api-client:1.1.1"
    implementation 'org.slf4j:slf4j-nop:1.7.35'
    // This follows advice from https://blog.gradle.org/log4j-vulnerability
    constraints {
        implementation("org.apache.logging.log4j:log4j-core") {
            version {
                strictly("[2.17, 3[")
                prefer("2.17.0")
            }
            because("CVE-2021-44228, CVE-2021-45046, CVE-2021-45105: Log4j vulnerable to remote code execution and other critical security vulnerabilities")
        }
    }

}
